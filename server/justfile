set export
set dotenv-load

SERVER_PORT := "8000"
DATABASE_PORT := "5432"
CONTAINER_NAME := "davs"
DATABASE_NAME := "davs_db"
LOCAL_DATABASE_PASSWORD := "secure-password"
LOCAL_DATABASE_USER := "davs-user"

# List available commands
default:
    just --list --unsorted --list-heading $'Available commands\n'

# Run main Docker container
run: stop-and-remove-container
    #!/bin/zsh

    DSN="host=host.docker.internal user=$LOCAL_DATABASE_USER password=$LOCAL_DATABASE_PASSWORD dbname=$DATABASE_NAME port=$DATABASE_PORT sslmode=disable TimeZone=Europe/Amsterdam"
    SERVER_ADDRESS="0.0.0.0:$SERVER_PORT"

    docker run -dp $SERVER_PORT:$SERVER_PORT --name $CONTAINER_NAME \
        -e DSN=$DSN -e SERVER_ADDRESS=$SERVER_ADDRESS \
        -e GIN_MODE="release" -e JWT_SECRET="jwt-secret+123" \
        -e API_KEY="$API_KEY" \
        $CONTAINER_NAME

# Build main Docker container
build:
    docker build -t $CONTAINER_NAME .

# Run unit tests
test:
    go test ./...

# Build and run main Docker container
build-run: build run

# Run application in dev mode
run-dev:
    #!/bin/zsh

    export SERVER_ADDRESS="127.0.0.1:$SERVER_PORT"
    export GIN_MODE="debug"
    export DATABASE_HOST="davs-db"
    export DATABASE_USER="davs-user"
    export DATABASE_PASSWORD="secure-password"
    export DATABASE_SSLMODE="disable"
    export JWT_SECRET="jwt-secret+123"

    reflex -r "\.go" -s -- sh -c "go run *.go run-server"

# Go mod tidy
go-mod-tidy:
    go mod tidy

bootstrap: setup-go-environment go-mod-tidy

[private]
setup-go-environment:
    go install github.com/cespare/reflex@latest

[private]
stop-and-remove-container:
    docker stop $CONTAINER_NAME || true
    docker rm $CONTAINER_NAME || true
